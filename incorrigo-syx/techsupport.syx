;;; INCORRIGO SYX DIGITAL COMMUNICATION SYSTEMS
;; TECHNICAL SUPPORT MANAGER
;; 
;; This is an optional feature that is switched
;; OFF by default (first use / factory reset)
;;
;; From the point where this feature is released
;; users at level 50 or higher have special status
;; but the features have to be enabled

;;; Explanations
alias explain.ra {
  ;; explanations that are context-specific when the user happens across one of the features to explain
  if ($$1 == nicklist) {
    noop $decision(gio¿incorrigo syx $pipe remote assist¿You can give or take the permission to entrust $$1 at a level that can give you remote assistance $&
      $+ $crlf $crlf $+ This trust is given to the user address so you would have to actually be $$1 to provide remote assistance $&
      $+ $crlf $crlf $+ Even stil $+ $c you need to enable specific commands or no one will be permitted to use them no matter what level they are)
  }

  if ($$1 == channel) {
    noop $decision(oig¿incorrigo syx $pipe remote assist¿This is a feature that was added at version 2.1a8 which gives you the remote assist capability $&
      $+ $crlf $crlf $+ You have to enable remote assist $+ $c it is off by default. Even when you switch it on $+ $c you have to enable specific commands or it does nothing)
  }

  if ($$1 == status) {
    noop $decision(oig¿incorrigo syx $pipe remote assist¿Remote assist is a feature that enables users you have trusted to do things remotely over your IRC session $&
      $+ $crlf $crlf $+ The settings for remote assist and the options in the channel / nick list will appear if you choose to show remote assist in this menu)
  }
}

dialog ra {
  title "incorrigo syx | remote assist settings"
  size -1 -1 247 117
  option dbu
  box "Remote Assist Manager", 1, 4 6 239 88
  check "Enable Remote Assist", 2, 9 17 65 10
  check "Input Requests", 3, 15 30 55 10, disable
  check "User list level control", 4, 15 40 70 10, disable
  check "Set + Return Variables", 5, 15 50 70 10, disable
  check "Send Channel / Query / Notice Messages", 6, 15 60 115 10, disable
  check "Full Command Access", 7, 15 70 70 10, disable
  text "message boxes pop up on the screen asking for your input answers", 8, 70 31 169 8, hide disable
  text "can add / remove to any user list level (even remote level)", 9, 91 40 148 8, hide disable
  text "can set / change / see value of any variables (your settings)", 10, 89 50 150 8, hide disable
  text "which will be from you and in your name", 11, 133 60 107 8, hide disable
  text "assistant will be able to issue both mIRC and server commands which means that they will have absolute full control of your session", 12, 90 70 150 21, hide disable
  button "E&xit", 13, 197 99 45 12, default flat ok
}


alias ram {
  if ($dialog(ra) == $null) {
    dialog -muo ra ra
  }
}

on *:DIALOG:ra:init:0: {
  if (%remote.assist == $true) {
    did -ec ra 2
    did -ue ra 3-7
    did -bv ra 8-12

    if (%ra.box) {
      did -c ra 3
      did -e ra 8
    }

    if (%ra.userlev) {
      did -c ra 4
      did -e ra 9
    }

    if (%ra.evar) {
      did -c ra 5
      did -e ra 10
    }

    if (%ra.message) {
      did -c ra 6
      did -e ra 11
    }

    if (%ra.llcmd) {
      did -c ra 7
      did -e ra 12
    }
  }
}

on *:DIALOG:ra:sclick:2: {
  if ($did(ra, 2).state == 1) {
    %ra.evar = $false
    %ra.box = $false
    %ra.userlev = $false
    %ra.message = $false
    %ra.llcmd = $false
    %remote.assist = $true
    did -ue ra 3-7
    did -bv ra 8-12
  }

  if ($did(ra, 2).state == 0) {
    %ra.evar = $false
    %ra.box = $false
    %ra.userlev = $false
    %ra.message = $false
    %ra.llcmd = $false
    %remote.assist = $false
    did -ub ra 3-7
    did -bh ra 8-12
  }
}

on *:DIALOG:ra:sclick:3: {
  if ($did(ra, 3).state == 1) {
    %ra.box = $true
    did -e ra 8
  }

  if ($did(ra, 3).state == 0) {
    %ra.box = $false
    did -b ra 8
  }
}

on *:DIALOG:ra:sclick:4: {
  if ($did(ra, 4).state == 1) {
    %ra.userlev = $true
    did -e ra 9
  }

  if ($did(ra, 4).state == 0) {
    %ra.userlev = $false
    did -b ra 9
  }
}

on *:DIALOG:ra:sclick:5: {
  if ($did(ra, 5).state == 1) {
    %ra.evar = $true
    did -e ra 10
  }

  if ($did(ra, 5).state == 0) {
    %ra.evar = $false
    did -b ra 10
  }
}

on *:DIALOG:ra:sclick:6: {
  if ($did(ra, 6).state == 1) {
    %ra.message = $true
    did -e ra 11
  }

  if ($did(ra, 6).state == 0) {
    %ra.message = $false
    did -b ra 11
  }
}

on *:DIALOG:ra:sclick:7: {
  if ($did(ra, 7).state == 1) {
    %ra.llcmd = $true
    did -e ra 12
  }

  if ($did(ra, 7).state == 0) {
    %ra.llcmd = $false
    did -b ra 12
  }
}


;;; REMOTE ASSIST user level command event handler
on ^50:NOTICE:.*:?: {
  if (%remote.assist == $true) {

    if ($window(@assist. $+ $nick) == $null) {
      ;; creates user-specific remote assist command console [unique per user]
      window -ni @assist. $+ $nick
      background -f @assist. [ $+ [ $nick ] ] incorrigo-syx\workaround.bmp
      echo 9 -t @assist. [ $+ [ $nick ] ] Incoming remote assist requests from  $+ $nick $+ , to  $+ $me $+ :
    }

    ;;; set false to any command type that has not been configured
    if (%ra.box == $null) { %ra.box = $false }
    if (%ra.message == $null) { %ra.message = $false }
    if (%ra.userlev == $null) { %ra.userlev = $false }
    if (%ra.evar == $null) { %ra.evar = $false }
    if (%ra.llcmd == $null) { %ra.llcmd = $false }

    ;;; output command issued [whether enabled or not] and all argument(s)
    haltdef
    echo 9 -t @assist. $+ $nick  $+ $nick $+ : $$1-

    ;;; authorisation check / version
    if ($1 == .ra-check) {
      ;; checks for user level 50 and remote assist version
      notice $nick [remote assist] authorised: incorrigo syx remote assist version 2025.01
      halt
    }

    if ($1 == .ra-msg) {
      ;; display message from assisting user in current window
      ;; [NOT] visible to other users
      echo -atc Info2  $+ $nick $+ : $$2-
      .notice $nick [remote assist] message displayed
      halt
    }

    if (%ra.box) {
      if ($1 == .box) {
        ;; creates and displays a message box from assistant
        %ra.box.type = gvf
        %ra.text = select a response

        ;; parameter - prompt icon
        if ($$2 == none) {
          goto goto3
        }
        if ($$2 == info) {
          %ra.box.type = %ra.box.type $+ i
          goto goto3
        }
        if ($$2 == warn) {
          %ra.box.type = %ra.box.type $+ w
          goto goto3
        }
        if ($$2 == halt) {
          %ra.box.type = %ra.box.type $+ h
          goto goto3
        }
        if ($$2 == question) {
          %ra.box.type = %ra.box.type $+ q
          goto goto3
        }
        if ($$2 == star) {
          %ra.box.type = %ra.box.type $+ t
          goto goto3
        }

        if (($$2 != none) && ($$2 != info) && ($$2 != warn) && ($$2 != halt) && ($$2 != question) && ($$2 != star)) {
          .notice $nick [remote assist] syntax error! .box <none $pipe info $pipe warn $pipe halt $pipe question> <default $pipe ok $pipe yn $&
            $pipe ync $pipe retry> <buttons $pipe edit $pipe password> Your input request message goes here
          echo 9 -t @assist. $+ $nick  $+ $nick $+  just made a syntax error [message box type]
          halt
        }
        :goto3

        ;; parameter - response buttons
        if ($$3 == default) {
          ;; just an OK button
          %ra.box.type = %ra.box.type $+ o
          goto goto4
        }

        if ($$3 == ok) {
          ;; just an OK button
          %ra.box.type = %ra.box.type $+ o
          goto goto4
        }

        if ($$3 == yn) {
          ;; yes/no buttons
          %ra.box.type = %ra.box.type $+ y
          goto goto4
        }

        if ($$3 == ync) {
          ;; yes/no/cancel
          %ra.box.type = %ra.box.type $+ n
          goto goto4
        }

        if ($$3 == retry) {
          ;; retry/cancel buttons
          %ra.box.type = %ra.box.type $+ r
        }

        if (($$3 != default) && ($$3 != ok) && ($$3 != yn) && ($$3 != ync) && ($$3 != retry)) {
          .notice $nick [remote assist] syntax error! .box <none $pipe info $pipe warn $pipe halt $pipe question> <default $pipe ok $pipe yn $&
            $pipe ync $pipe retry> <buttons $pipe edit $pipe password> Your input request message goes here
          echo 9 -t @assist. $+ $nick  $+ $nick $+  just made a syntax error [message box buttons]
          halt
        }
        :goto4

        ;; parameter - response type
        if ($$4 == buttons) {
          ;; just click a button
          %ra.box.type = %ra.box.type $+ b
          goto goto5
        }

        if ($$4 == edit) {
          ;; type in a response
          %ra.box.type = %ra.box.type $+ be
          goto goto5
        }

        if ($$4 == password) {
          ;; password response mode (not hidden from response)
          %ra.box.type = %ra.box.type $+ bp
          goto goto5
        }

        if (($$4 != buttons) && ($$4 != edit) && ($$4 != password)) {
          .notice $nick [remote assist] syntax error! .box <none $pipe info $pipe warn $pipe halt $pipe question> <default $pipe ok $pipe yn $&
            $pipe ync $pipe retry> <buttons $pipe edit $pipe password> Your input request message goes here
          echo 9 -t @assist. $+ $nick  $+ $nick $+  just made a syntax error [invalid message box input method]
          halt
        }
        :goto5

        ;; parameter - prompt text
        if ($$5) {
          %ra.box.text = $$5-
        }

        .timer -m 1 100 inputrequest $nick
        halt
      }
    }

    if (!%ra.box && $1 == .box) {
      ;; user has not enabled input requests
      notice $nick [remote assist] access denied: .box commands have not been enabled
      echo 9 -t @assist. $+ $nick Access denied! An input request from  $+ $nick $+  was declined. You have not enabled input requests
      halt
    }

    if (%ra.message) {
      ;; send to channel / query / notice
      if ($1 == .send) {
        ;; query / channel message
        msg $$2 $$3-
        .notice $nick [remote assist] sent to  $+ $2 $+ : $3-
        halt
      }

      if ($1 == .tell) {
        ;; same as .send just a notice instead
        notice $$2 $$3-
        .notice $nick [remote assist] notice'd  $+ $2 $+ : $3-
        halt
      }
    }

    if (!%ra.message && $1 == .send) {
      ;; user has not enabled privmsg requests
      .notice $nick [remote assist] access denied: send message commands have not been enabled
      echo 9 -t @assist. $+ $nick Access denied! User  $+ $nick $+  attempted .send ($2-)
      halt
    }

    if (!%ra.message && $1 == .tell) {
      ;; user has not enabled notice requests
      .notice $nick [remote assist] access denied: send notice commands have not been enabled
      echo 9 -t @assist. $+ $nick Access denied! User  $+ $nick $+  attempted .tell ($2-)
      halt
    }

    if (%ra.userlev) {
      ;; remote assistant user level management commands  
      if ($1 == .user) {
        if ($$2 == add) {
          ;; adds a new entry to the user list
          auser $$3-
          .notice $nick [remote assist] added user  $+ $$4 $+  to level  $+ $$3 $+ 
          halt
        }

        if ($$2 == also) {
          ;; adds a new entry to the user list
          auser -a $$3-
          .notice $nick [remote assist] included level  $+ $$3 $+  to user entry  $+ $$4 $+ 
          halt
        }

        if ($$2 == del) {
          ;; adds a new entry to the user list
          ruser $$3-
          .notice $nick [remote assist] removing user list item matching  $+ $$4 $+  from level  $+ $$3 $+ 
          halt
        }

        if (($$2 != add) && ($$2 != del) && ($$2 != also)) {
          ;; user level instruction syntax error
          .notice $nick [remote assist] syntax error! .user <add $pipe del $pipe also> <level> <nick $pipe address>
          echo 9 -t @assist. $+ $nick  $+ $nick $+  just made a syntax error [invalid user level instruction]
        }
      }
    }

    if (!%ra.userlev && $$1 == .user) {
      echo 9 -t @assist. $+ $nick User level command from  $+ $nick $+  was denied (user level commands disabled)
      notice $nick [remote assist] access denied: user level commands have not been enabled
    }

    if (%ra.evar) {
      if ($1 == .evar) {
        if ($$2 == -e) {
          set -e [ % $+ [ $$3 ] ] $$4-
          echo 9 -t @assist. $+ $nick Session only variable set by  $+ $nick $+  called  $+ $$3 $+  set to $$4-
        }

        if ($$2 == set) {
          set [ % $+ [ $$3 ] ] $$4-
          echo 9 -t @assist. $+ $nick Variable set by  $+ $nick $+  called  $+ $$3 $+  set to $$4-
        }

        if ($$2 == unset) {
          unset [ % $+ [ $$3 ] ] $$4-
          echo 9 -t @assist. $+ $nick Unsetting variable -  $+ $nick $+  unset variable  $+ $$3 $+ 
        }

        if ($$2 == get) {
          .notice $nick [remote assist] variable - value of % $+ $$3 $+  is: [ % $+ [ $$3 ] ]
        }

        if (($$2 != -e) && ($$2 != set) && ($$2 != unset) && ($$2 != get) {
          ;; variable assignment syntax error
          .notice $nick [remote assist] syntax error! .evar <-e $pipe set $pipe unset $pipe get> <variable> [assignment]
          echo 9 -t @assist. $+ $nick  $+ $nick $+  just made a syntax error [invalid user level instruction]
        }
      }
    }

    if (!%ra.evar && $$1 == .evar) {
      echo 9 -t @assist. $+ $nick Varaiable control command from  $+ $nick $+  was denied (variable controls disabled)
      notice $nick [remote assist] access denied: variable control commands have not been enabled
    }
  }
}

alias -l inputrequest {
  .timer -m 1 1 .notice $$1 [remote assist] response: $decision(%ra.box.type $+ ¿incorrigo syx $pipe remote assist¿ $+ %ra.box.text)
  .timer -m 1 50 echo 9 -t @assist. $+ $1 Your input response has been sent to  $+ $$1 $+ 
}

on ^*:NOTICE:.*:?: {
  if (%remote.assist == $true) {
    haltdef
    echo -atc Info * Unauthorised command from  $+ $nick $+ : $$1-
  }
}


;;; script display settings dialog
dialog sd {
  title "incorrigo syx | display options"
  size -1 -1 200 136
  option dbu
  box "Options Autosave", 1, 4 2 190 115
  button "E&xit", 2, 149 120 45 12, default flat ok
  text "Options auto-save when they are changed", 3, 4 122 140 8
  check "Read Out Permission Changes [Simplified Mode Change Read-out]", 4, 8 15 169 10, flat
  combo 5, 67 29 80 50, size vsbar drop
  text "Light / Dark Theme:", 6, 8 30 51 8
  text "Local Time Mode:", 7, 8 42 51 8
  combo 8, 67 42 80 50, size vsbar drop
  edit "Loading Local Time", 9, 66 54 124 12, read limit 999 autohs
  text "Local Time Example:", 10, 8 55 57 8
  box "Menu Visibility", 11, 8 66 183 30
  check "Half-Op", 12, 11 74 30 10, flat
  check "Chan Op", 13, 43 74 33 10, flat
  check "Registered User", 14, 76 74 51 10, flat
  check "Identified Nickname", 15, 129 74 57 10, flat
  check "Network Engineer", 16, 11 85 58 10, flat
  check "Show Remote Assistance Options", 17, 8 98 94 10, flat
}

alias sd {
  if (!$dialog(sd)) {
    if ($me ison $$1) {
      set -e %caps.ch $$1
      noop $dialog(sd, sd, u)
    }
  }
}

on *:DIALOG:sd:init:0: {
  ;; return smc setting
  if ($issmc) { did -c sd 4 }

  ;; generate combo options
  ;; light / dark settings
  did -ra sd 5 Auto Load
  did -a sd 5 Light Theme (original)
  did -a sd 5 Dark Theme (new)

  if ($dark.mode == $true) { did -c sd 5 3 }
  if ($dark.mode == $false) { did -c sd 5 2 }
  if ($dark.mode == auto) { did -c sd 5 1 }

  ;; local time settings
  did -ra sd 8 Very Long Time & Date
  did -a sd 8 Long Time & Date
  did -a sd 8 Local Time Only
  did -a sd 8 Brief Time & Date
  did -a sd 8 Time of Day & Date
  if (%ctcp.timeformat == $null) { %ctcp.timeformat = 2 }
  if ($timetf(1)) { did -c sd 8 1 }
  if ($timetf(2)) { did -c sd 8 2 }
  if ($timetf(3)) { did -c sd 8 3 }
  if ($timetf(4)) { did -c sd 8 4 }
  if ($timetf(5)) { did -c sd 8 5 }
  did -ra sd 9 $strip($gettime)

  ;; menu visibility
  did -ra sd 11 Menu Visibility: %caps.ch
  if ($wzy(%caps.ch) == $null) { did -c sd 12 }
  if ($wdy(%caps.ch) == $null) { did -c sd 13 }
  if ($idf == $true) { did -c sd 14 }
  if ($idfr == $true) { did -c sd 15 }
  if ($sysop) { did -c sd 16 }

  ;; show remote assist
  if (%remote.assist == $null) { %remote.assist = $false }
  if (%remote.assist == $true) { did -c sd 17 }
}

on *:DIALOG:sd:sclick:4: {
  togglesmc
}

on *:DIALOG:sd:sclick:5: {
  if ($did(sd, 5).sel == 1) { goauto }
  if ($did(sd, 5).sel == 2) { golight }
  if ($did(sd, 5).sel == 3) { godark }
}

on *:DIALOG:sd:sclick:8: {
  %ctcp.timeformat = $did(sd, 8).sel
  did -ra sd 9 $strip($gettime)
}

on *:DIALOG:sd:sclick:12: {
  if ($wzy(%caps.ch) == $null) { did -c sd 12 }
  if ($wzy(%caps.ch) != $null) { did -u sd 12 }
}

on *:DIALOG:sd:sclick:13: {
  if ($wdy(%caps.ch) == $null) { did -c sd 13 }
  if ($wdy(%caps.ch) != $null) { did -u sd 13 }
}

on *:DIALOG:sd:sclick:14: {
  if ($idf == $true) { did -c sd 14 }
  if ($idf != $true) { did -u sd 14 }
}

on *:DIALOG:sd:sclick:15: {
  if ($idfr == $true) { did -c sd 15 }
  if ($idfr != $true) { did -u sd 15 }
}

on *:DIALOG:sd:sclick:16: {
  if ($sysop) { did -c sd 16 }
  if (!$sysop) { did -u sd 16 }
}

on *:DIALOG:sd:sclick:16: {
  if ($did(sd, 16) == 1) { %remote.assist = $true }
  if ($did(sd, 16) == 0) { %remote.assist = $false }
}

dialog rah {
  title "incorrigo syx | remote assist manager"
  size -1 -1 201 100
  option dbu
  tab "Enable", 1, 6 3 190 78
  box "Important", 2, 8 19 183 56, tab 1
  text "If you switch remote assist off ... no one will be able to give you any remote assistance. Even if you have given them permission, or they have a level of 50 or higher on your user list", 3, 16 30 167 22, tab 1
  combo 4, 47 54 97 50, tab 1 size drop
  link "User Levels", 77, 152 57 33 8, tab 1
  tab "IRC Session", 5
  check "(Select All)", 7, 10 22 50 10, tab 5 flat
  check "Private Message", 9, 15 42 50 10, tab 5 flat
  check "Channel Message", 8, 15 32 56 10, tab 5 flat
  text "No-one can read your mesages", 10, 7 64 88 8, disable tab 5 center
  check "Nickserv", 11, 103 29 40 10, tab 5 flat
  text "Run Services Commands", 12, 98 19 92 8, tab 5
  check "Chanserv", 13, 103 37 40 10, tab 5 flat
  check "Hostserv", 14, 103 45 40 10, tab 5 flat
  check "Memoserv", 15, 103 53 40 10, tab 5 flat
  check "Botserv", 16, 103 61 40 10, tab 5 flat
  check "Operserv", 17, 103 69 40 10, tab 5 flat
  check "Tag Messages", 21, 15 52 50 10, tab 5 flat
  link "[?]", 52, 146 30 14 8, tab 5
  link "[?]", 53, 146 38 14 8, tab 5
  link "[?]", 54, 146 46 14 8, tab 5
  link "[?]", 55, 146 54 14 8, tab 5
  link "[?]", 56, 146 62 14 8, tab 5
  link "[?]", 57, 146 70 14 8, tab 5
  link "[?]", 71, 75 34 14 8, tab 5
  link "[?]", 72, 75 43 14 8, tab 5
  link "[?]", 73, 75 52 14 8, tab 5
  tab "Values", 18
  text "Session Variables are deleted when mIRC closes", 19, 23 21 155 8, tab 18 center
  check "Set Variables", 20, 19 38 60 10, tab 18 flat
  check "Connection Specific", 23, 19 56 60 10, tab 18 flat
  check "Session Variables", 22, 19 47 60 10, tab 18 flat
  box "Declare Variables", 24, 12 29 69 40, tab 18
  box "Get Current Values", 25, 85 29 96 50, tab 18
  check "Get Variable Value", 26, 90 37 58 10, tab 18 flat
  check "Identifier Return Value", 27, 90 46 67 10, tab 18 flat
  text "Local Commands Only", 29, 90 66 86 8, disable tab 18 center
  check "Use Values in Command", 28, 90 55 69 10, tab 18 flat
  link "[?]", 58, 162 37 14 8, tab 18
  link "[?]", 59, 162 45 14 8, tab 18
  link "[?]", 60, 162 53 14 8, tab 18
  link "Variables and Types", 78, 18 70 54 8, tab 18
  tab "Prompts", 30
  text "These prompts are used to gather useful information quickly", 31, 22 61 154 8, tab 30
  check "Enable Input Prompting", 32, 11 21 70 10, tab 30 flat
  icon 33, 28 33 24 24,  incorrigo-syx\img\icon.png, 0, tab 30
  icon 37, 140 33 24 24,  incorrigo-syx\img\sysop.png, 0, tab 30
  icon 35, 84 33 24 24,  incorrigo-syx\img\triangle.png, 0, tab 30
  icon 34, 56 33 24 24,  incorrigo-syx\img\question.png, 0, tab 30
  icon 36, 112 33 24 24,  incorrigo-syx\img\cross.png, 0, tab 30
  text "while they can be annoying if you're preoccupied with something else", 38, 11 71 166 8, tab 30 center
  link "[?]", 61, 82 22 14 8, tab 30
  tab "Executive", 39
  check "Use /RAW", 40, 13 23 37 10, tab 39 flat
  check "Get URL to file", 43, 13 58 49 10, tab 39 flat
  check "Kick && Ban From Channel", 45, 100 20 71 10, tab 39 flat
  check "Load Remote Scripts", 44, 13 67 64 10, tab 39 flat
  check "Use /RUN", 41, 13 32 37 10, tab 39 flat
  check "Master Command", 42, 13 41 57 10, tab 39 flat
  text "(Run any command)", 51, 22 50 64 8, disable tab 39
  check "/OPER up", 46, 100 29 37 10, tab 39 flat
  check "Set Channel Modes", 47, 100 38 57 10, tab 39 flat
  check "Set User Modes", 48, 100 47 50 10, tab 39 flat
  check "Change Nickname", 49, 100 56 55 10, tab 39 flat
  check "IRC Operator Commands", 50, 100 65 72 10, tab 39 flat
  link "[?]", 62, 176 21 14 8, tab 39
  link "[?]", 67, 176 65 14 8, tab 39
  link "[?]", 66, 176 56 14 8, tab 39
  link "[?]", 65, 176 47 14 8, tab 39
  link "[?]", 64, 176 39 14 8, tab 39
  link "[?]", 63, 176 30 14 8, tab 39
  link "[?]", 70, 72 41 14 8, tab 39
  link "[?]", 68, 72 24 14 8, tab 39
  link "[?]", 69, 72 32 14 8, tab 39
  link "[?]", 74, 81 59 14 8, tab 39
  link "[?]", 75, 81 67 14 8, tab 39
  text "Incorrigo Syx", 6, 6 86 119 8
  button "D&one", 76, 147 85 50 12, default flat multi ok
}


alias remote.assist {
  if (!$dialog(rah)) {
    noop $dialog(rah, rah, u)
  }
}

on *:DIALOG:rah:init:0: {
  did -a rah 4 Enabled
  did -a rah 4 Disabled
  if (!%remote.assist) {
    did -c rah 4 2
  }

  else {
    did -c rah 4 1
  }

  if (!$exists(incorrigo-syx\chicken-oriental\rah.syx)) {
    init-rah
  }
}

alias init-rah {
  ;; this will load rah.syx with the default values which
  ;; are all 0
  %rf = incorrigo-syx\chicken-oriental\rah.syx

  ;; session
  rah session private 0
  rah session channel 0
  rah session tag 0
  rah session nickserv 0
  rah session chanserv 0
  rah session hostserv 0
  rah session memoserv 0
  rah session botserv 0
  rah session operserv 0

  ;; values
  rah var set 0
  rah var connection 0
  rah var session 0
  rah var getvalue 0
  rah var return 0
  rah var useval 0

  ;; prompt
  rah prompts prompting 0

  ;; executive
  rah scr useraw 0
  rah scr geturl 0
  rah scr kickban 0
  rah scr loadscript 0
  rah scr userun 0
  rah scr cmdmaster 0
  rah scr oper 0
  rah scr chmodes 0
  rah scr umodes 0
  rah scr chnick 0
  rah scr opercmds 0
}

alias -l rah {
  if (!%rf) { %rf = incorrigo-syx\chicken-oriental\rah.syx }
  writeini %rf $$1-
}

;;; settings memory
;; 01: SESSION
on *:DIALOG:rah:sclick:9: {
  rah session private $did(rah, 9).state
}

on *:DIALOG:rah:sclick:8: {
  rah session channel $did(rah, 8).state
}

on *:DIALOG:rah:sclick:11: {
  rah session nickserv $did(rah, 11).state
}

on *:DIALOG:rah:sclick:13: {
  rah session chanserv $did(rah, 13).state
}

on *:DIALOG:rah:sclick:14: {
  rah session hostserv $did(rah, 14).state
}

on *:DIALOG:rah:sclick:16: {
  rah session botserv $did(rah, 16).state
}

on *:DIALOG:rah:sclick:17: {
  rah session operserv $did(rah, 17).state
}

on *:DIALOG:rah:sclick:15: {
  rah session memoserv $did(rah, 15).state
}

on *:DIALOG:rah:sclick:21: {
  rah session tag $did(rah, 21).state
}

;; 02: VALUES

on *:DIALOG:rah:sclick:20: {
  rah values set $did(rah, 20).state
}

on *:DIALOG:rah:sclick:23: {
  rah values connection $did(rah, 23).state
}
on *:DIALOG:rah:sclick:22: {
  rah values session $did(rah, 22).state
}
on *:DIALOG:rah:sclick:26: {
  rah values getvalue $did(rah, 26).state
}
on *:DIALOG:rah:sclick:27: {
  rah values return $did(rah, 27).state
}
on *:DIALOG:rah:sclick:28: {
  rah values useval $did(rah, 28).state
}

;; 03: PROMPTS
;; (only one setting really)

on *:DIALOG:rah:sclick:32: {
  rah prompts prompting $did(rah, 32).state
}

;; 04: EXECUTIVE

on *:DIALOG:rah:sclick:40: {
  rah scr useraw 
}

on *:DIALOG:rah:sclick:4: {
  if ($did(rah, 4).sel == 1) {
    %remote.assist = $true
  }

  if ($did(rah, 4).sel == 2) {
    %remote.assist = $false
  }
}

;; Info Buttons
on *:DIALOG:rah:sclick:77: {
  noop $decision(iugo¿incorrigo syx $pipe user levels¿Users have a numeric level that goes higher the more you trust someone $&
    $+ $crlf $crlf $+ Unknown / new users all start at the default level $+ $c which is level 1 $+ $crlf $crlf $+ $&
    User levels are a key feature of mIRC which we have introduced into the latest release as the remote assist user list $&
    $+ $crlf $crlf $+ Someone who is given remote assist permissions is set to level 50)
}

on *:DIALOG:rah:sclick:71: {
  ;; tab 2 > channel messages
  noop $decision(iugo¿incorrigo syx $pipe irc assist¿Allow the remote assist system to message channels that you are on $+ $crlf $crlf $+ $&
    Because nearly all channels in the world are +n (no external messages) even you would probably not accomplish this otherwise)
}

on *:DIALOG:rah:sclick:72: {
  ;; tab 2 > private messages
  noop $decision(iugo¿incorrigo syx $pipe irc assist¿If this is enabled, remote assistant will be able to open up query windows to send private messages $&
    $+ $crlf $crlf $+ You will be able to see the messages that are sent $+ $c and who it has been sent to)
}

on *:DIALOG:rah:sclick:73: {
  ;; tab 2 > tag messages
  noop $decision(iugo¿incorrigo syx $pipe irc assist¿There are unseen message tags mostly generated by the server in a vast number of things $+ $crlf $crlf $+ $&
    There is also a command that sends _only_ tags $+ $c with no message. As with all the other messages - you will be able to see what is sent and the recipient)
}


on *:DIALOG:rah:sclick:52: {
  ;; tab 2 > nickserv
  noop $decision(iugo¿incorrigo syx $pipe irc assist¿This option will allow remote assist to issue /nickserv commands $&
    $+ $crlf $crlf $+ nickserv is the user account registration and management service. you need to have an account with $&
    nickserv to use any of the other network services)
}

on *:DIALOG:rah:sclick:53: {
  ;; tab 2 > chanserv
  noop $decision(iugo¿incorrigo syx $pipe irc assist¿This option will allow remote assist to issue /chanserv commands $&
    $+ $crlf $crlf $+ chanserv is the network service that maintains the settings and ownership of registered channels)
}

on *:DIALOG:rah:sclick:54: {
  ;; tab 2 > hostserv
  noop $decision(iugo¿incorrigo syx $pipe irc assist¿This option will allow remote assist to issue /hostserv commands $&
    $+ $crlf $crlf $+ hostserv handles vanity hostname (vHost) assignment for registered users $+ $c including vHost requests and settings)
}

on *:DIALOG:rah:sclick:55: {
  ;; tab 2 > memoserv
  noop $decision(iugo¿incorrigo syx $pipe irc assist¿This option will allow remote assist to issue /memoserv commands $&
    $+ $crlf $crlf $+ memoserv provides an inbox for registered users - you also have an inbox for channels - and the $&
    settings / functions used for handling memo messages)
}

on *:DIALOG:rah:sclick:56: {
  ;; tab 2 > botserv
  noop $decision(iugo¿incorrigo syx $pipe irc assist¿This option will allow remote assist to issue /botserv commands $&
    $+ $crlf $crlf $+ botserv is a network service that lets registered channels have a bot that joins the channel and $&
    provides an interactive range of extra features $ampersand settings $+ $crlf $crlf $+ When your channel has a bot $+ $c $&
    it is operated by services so it has all the powers and protection that chanserv does. $+ $crlf $crlf $+ Type !help in $&
    the channel where the bot has joined $+ $c to receive a list of commands you can use to manage the channel)
}

on *:DIALOG:rah:sclick:57: {
  ;; tab 2 > operserv
  noop $decision(iugo¿incorrigo syx $pipe irc assist¿This option will allow remote assist to issue /operserv commands $&
    $+ $crlf $crlf $+ operserv is the executive service for the network manager - and can only be used by a services operator $+ $c $&
    no matter what permissions you or the remote user may currently have)
}

on *:DIALOG:rah:sclick:58: {
  ;; tab 3 > get variable value
  noop $decision(iugo¿incorrigo syx $pipe data assist¿Enabling this will allow remote assistance to ask for the current value of a variable)
}
on *:DIALOG:rah:sclick:59: {
  ;; tab 3 > identifier return value
  noop $decision(iugo¿incorrigo syx $pipe data assist¿This setting will allow remote assist to retrieve the results of identifiers $&
    $+ $crlf $crlf $+ Identifiers are different from variables mainly because the value returned by an identifier is the result of a command that is run)
}
on *:DIALOG:rah:sclick:60: {
  ;; tab 3 > use values in command
  noop $decision(iugo¿incorrigo syx $pipe data assist¿Aliases [local commands] can be manufactured to use variables and produce identifiers $&
    $+ $crlf $crlf $+ Enabling this setting allows remote assist to put the variables / identifiers into small aliases so that the values can be used to solve a problem $&
    $+ $crlf $crlf $+ This is the very essence of what scripting is and how it all fits together)
}

on *:DIALOG:rah:sclick:61: {
  ;; tab 4 > icon
  noop $decision(iugo¿incorrigo syx $pipe asking and telling¿99.9% of asking for assistance on the internet consists of gathering information for diagnostic purposes $&
    $+ $crlf $crlf $+ Input and information prompts are a very useful way of gathering details quickly ... just remember to turn them off when you're done cos there's always one ...)
}

on *:DIALOG:rah:sclick:33: {
  ;; tab 4 > icon
  noop $decision(iugo¿incorrigo syx $pipe information¿A user who is assisting you remotely can send you an information prompt like this one $+ $c $&
    which tells you something no matter what is currently going on $+ $crlf $crlf $+ A message like this one is an effective line of communication so you $&
    know that your remote assistant wants you to do something $+ $c or is )
}
on *:DIALOG:rah:sclick:37: {
  ;; tab 4 > star
  noop $decision(tugo¿incorrigo syx $pipe star¿If you get a message like this and you're still connected to the network then it's probably good news)
}
on *:DIALOG:rah:sclick:35: {
  ;; tab 4 > triangle
  noop $decision(wugo¿incorrigo syx $pipe triangle¿This means it's not exactly the end of the world but might be something you need to take a look at)
}
on *:DIALOG:rah:sclick:36: {
  ;; tab 4 > circle
  noop $decision(hugr¿incorrigo syx $pipe circle¿Something has not worked properly or you have been banned from the channel / network)
}

on *:DIALOG:rah:sclick:34: {
  ;; tab 4 > question symbol
  var %parole $r(0,1)
  noop $decision(iug $+ $iif(%parole == 1,p,e) $+ ¿incorrigo syx $pipe question mark¿What would you say this kind of prompt is used for?)
}

on *:DIALOG:rah:sclick:68: {
  ;; tab 5 > use /RAW
  noop $decision(ugo¿incorrigo syx $pipe executive assist¿Some IRC commands are actually handled by mIRC and not sent to the server as-is $&
    $+ $crlf $crlf $+ When you use /RAW it sends the command [and all command arguments] straight to the server $+ $c bypassing the way it is $&
    handled by the client $+ $crlf $crlf $+ Enabling this feature can be very useful for remote assistance $+ $c because it allows the remote user $&
    to understand exactly what is being sent from you to the server)
}

on *:DIALOG:rah:sclick:69: {
  ;; tab 5 > use /RUN
  noop $decision(ugo¿incorrigo syx $pipe about /run¿What someone decides to do with remote access to the /RUN command can have connotations that affect $&
    the situation on the whole computer $+ $crlf $crlf $+ The /RUN command is used to execute a program - or use a program to execute a file stored somewhere $&
    on the computer $+ $crlf $crlf $+ This command enables a remote assistant to perform actions that can go much further beyond the scope of a mIRC related $&
    issue $+ $crlf $crlf $+ If you are going to enable this to be used remotely $+ $c you should only do so when it is needed $+ $c and there is a clear enough $&
    determination how it is going to be used)
}

on *:DIALOG:rah:sclick:70: {
  ;; tab 5 > Master Command
  noop $decision(ugo¿incorrigo syx $pipe master command¿Master command allows any command that can be typed in to be run remotely $+ $crlf $crlf $+ $&
    All the functions that can be enabled in this tab were designed to be the most useful $+ $c therefore the most powerful $+ $crlf $crlf $+ $&
    This is probably the main $&
    setting made available by remote assist - since in essence enabling this setting allows your trusted remote user to manually perform all the others)
}

on *:DIALOG:rah:sclick:74: {
  ;; tab 5 > Fetch File From URL
  noop $decision(ugo¿incorrigo syx $pipe fetch file from URL¿This will allow the remote user to fetch a [text] file from an https location and save it in a local folder $+ $crlf $crlf $+ $&
    As a remote assist function $+ $c it will not be available in the Incorrigo Syx 2.1a8 release. But it will be rolled out in subsequent updates to the repository ... $&
    where it will co-incide with other developments that are currently being designed)
}

on *:DIALOG:rah:sclick:75: {
  ;; tab 5 > Load Remote Scripts
  noop $decision(ugo¿incorrigo syx $pipe remote assist¿This feature will not be available in the 2.1a8 release ... and there will probably be a lot more to it $&
    as the remote assist system will be rolled out in subsequent updates to the repository $+ $crlf $crlf $+ By default - i.e. factory reset conditions - remote assist will not be visible. The $&
    purpose of the release is to introduce the main mIRC settings - then you can just overwrite script files as they update $+ $crlf $crlf $+ As we get used to github $+ $c future releases are going $&
    to address this. But for now $+ $c the settings and information are being released first)
}

on *:DIALOG:rah:sclick:62: {
  ;; tab 5 > kick and ban people
  noop $decision(ugo¿incorrigo syx $pipe executive commands¿Enabling this setting will let remote assist kick and ban people from channels you are an operator on)
}

on *:DIALOG:rah:sclick:63: {
  ;; tab 5 > become an IRC operator
  noop $decision(ugo¿incorrigo syx $pipe executive commands¿If you enable this setting then remote assist will accept attempts to log you in as an IRC operator with the credentials that are sent)

}

on *:DIALOG:rah:sclick:64: {
  ;; tab 5 > set channel modes
  noop $decision(ugo¿incorrigo syx $pipe executive commands¿When you are a channel operator and this setting is enabled $+ $c remote assist will accept commands to make mode changes)
}

on *:DIALOG:rah:sclick:65: {
  ;; tab 5 > set user modes
  noop $decision(ugo¿incorrigo syx $pipe executive commands¿On IRC you can only set user modes on yourself. However $+ $c if you trust the user[s] that you give remote access to - remote assist will respond to user mode commands by making such mode changes $&
    $+ $crlf $crlf $+ ... to the server $+ $c it's exactly the same)
}

on *:DIALOG:rah:sclick:66: {
  ;; tab 5 > change nickname
  noop $decision(ugo¿incorrigo syx $pipe executive commands¿If you want to let your remote assist log you into an account etc. then you should enable nickserv etc. in the "IRC Session" tab $&
    $+ $crlf $crlf $+ Enabling services commands is a good combination when you allow this setting - which will help your remote assist help you with account settings and login issues &c.)
}

on *:DIALOG:rah:sclick:67: {
  ;; tab 5 > use IRC operator commands
  noop $decision(ugo¿incorrigo syx $pipe executive commands¿Enabling this setting will let remote assist kick and ban people from channels you are an operator on)
}

on *:DIALOG:rah:sclick:78: {
  ;; tab 3 variables and types
  noop $decision(iugo¿incorrigo syx $pipe settings and values¿Variables are named containers where temporary / changeable values of data are stored $&
    $+ $crlf $crlf $+ Most of the script's settings are stored in variables; and the factory reset command basically involves deleting them all. $&
    There are a few types of variables that have different levels of importance in this script $+ $crlf $crlf $+ Variables - These can be set at any $&
    time and will remain there until the value of it is changed or the variable is deleted $+ $crlf $crlf $+ Session Variables - These variables will be $&
    automatically deleted when mIRC is closed so are only needed to temporarily store information in solving a specific problem $&
    $+ $crlf $crlf $+ Connection specific variables are only relevant to the current connection that is receiving remote assist commands for the purposes of $&
    multiple connections to the network at the same time (not perfect but is possible))
}
