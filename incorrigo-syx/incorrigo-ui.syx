;; INCORRIGO SYX DIGITAL COMMUNICATION SYSTEMS
;; UI MANAGER - EXTENDED BAN CREATOR
;; Designed for UnrealIRCd extended bans

dialog extban-creator {
  title "incorrigo syx | extended list mode creator"
  size -1 -1 287 185
  option dbu
  icon $mircdirincorrigo-syx\img\favicon.png, 0
  box "Extended Bans", 1, 6 2 277 164
  button "E&xit", 2, 217 169 65 12, default flat cancel
  check "Tell server to remove in", 3, 27 12 76 10, flat
  edit "28", 4, 105 12 17 12, disable limit 3
  text "minute(s)", 5, 124 14 28 8, disable
  box "Select Which Channel List", 6, 189 13 82 58
  radio "Ban List [+b]", 7, 201 23 65 12, group flat
  radio "Exception List [+e]", 8, 201 38 65 12, flat
  radio "Invite List Entry [+I]", 9, 201 53 65 12, flat
  box "Extended Ban Actions", 10, 11 23 164 73
  combo 11, 17 43 150 50, disable size vsbar drop
  check "Modify Ban Behaviour / Exception Activity", 12, 14 31 157 10, flat
  link "[?]", 13, 192 25 8 8
  link "[?]", 14, 192 40 8 8
  link "[?]", 15, 192 55 8 8
  box "Listing Type of Target", 16, 189 100 82 34
  radio "nick!cl@ssic.list.en.try", 17, 193 111 75 10, group flat
  radio "Extended Criteria Selection", 18, 193 120 75 10, flat
  edit "*!*@*.name.or.ip", 19, 16 117 155 12, autohs limit 93
  combo 20, 86 131 85 50, disable size drop
  box "Target User Mask / Nickname", 21, 13 98 163 61
  text "Country:", 22, 18 146 26 8, disable
  combo 23, 86 146 85 50, disable size drop
  text "Enter your target:", 24, 16 107 55 8
  text "Here is an example: ~time:28:~account:westid`", 25, 7 170 181 8
  combo 26, 17 66 150 35, disable size vsbar drop
  text "Subcategory of ban type or restricted mode exemption:", 27, 22 56 136 8
  text "Forward to:", 28, 30 82 29 8, disable
  edit "#channelname", 29, 62 80 95 12, read autohs limit 32
  text "Extended Target Types:", 30, 18 133 66 8, disable
  link "Targeting & Ban Lists", 31, 86 107 86 8
  box "Current Channel Name", 32, 189 72 82 27
  text "#antidisestablishmentarianism", 33, 194 83 72 8, center
  button "Set Now", 34, 195 145 70 12, hide flat ok
  box "Creation Complete?", 35, 189 136 82 26, hide
  link "copy text", 36, 185 170 30 8, hide
}


alias extbanner {
  %extban.channel = $$1
  noop $dialog(extban-creator, extban-creator, u)
}

alias extbannee.1 {
  .timer 1 1 did -ra extban-creator 19 $$2
  extbanner $$1 $$2
}

alias extbannee.1 {
  .timer 1 1 did -ra extban-creator 19 $$2
  extbanner $$1 $$2
}

on *:DIALOG:extban-creator:sclick:36: {
  clipboard /mode %extban.channel %extban.setmode
  .timer -m 1 150 noop $decision(ogu¿incorrigo syx $pipe command confirmed¿Following text: $+ $crlf $crlf $+ /mode %extban.channel %extban.setmode $+ $crlf $crlf $+ Has been copied to clipboard.)
}
on *:DIALOG:extban-creator:sclick:34: mode %extban.channel %extban.setmode

on *:DIALOG:extban-creator:init:0: {
  ;; Add Entries to Action List
  did -a extban-creator 11 [Ban] Prohibit Messages to Channel
  did -a extban-creator 11 [Ban] Cannot Change Nickname
  did -a extban-creator 11 [Ban] Forward to Channel
  did -a extban-creator 11 [Exempt] Bypass Message Restrictions
  did -a extban-creator 11 [Exempt] Immune to Flood Defences
  did -a extban-creator 11 [Other] Prohibit Join Only (Msg / Nick Change Permitted)

  ;; Check Default Selection
  did -c extban-creator 7
  did -c extban-creator 17
  set %extban.timed $false
  set %extban.timer 28
  set %extban.selector 1
  set %extban.action 1
  set %extban.listmode +b

  ;; Choice of Channel
  did -ra extban-creator 33 %extban.channel
}

on *:DIALOG:extban-creator:sclick:3: {
  if ($did(extban-creator,3).state == 1) {
    did -e extban-creator 4-5
    set %extban.timed $true
  }

  else {
    did -b extban-creator 4-5
    set %extban.timed $false
  }
  extban.cobbletogether
}

on *:DIALOG:extban-creator:edit:19: {
  if ($len($did(extban-creator, 19).text) > 0) {
    did -ra extban-creator 19 $remove($did(extban-creator, 19).text, $chr(32))
    extban.cobbletogether
  }
}

on *:DIALOG:extban-creator:edit:29: {
  if ($len($did(extban-creator, 29).text) > 0) {
    did -ra extban-creator 29 $remove($did(extban-creator, 29).text, $chr(32))
    extban.cobbletogether
  }
}

on *:DIALOG:extban-creator:sclick:17: {
  ;; Remove Extended Selectors
  did -b extban-creator 20,30
  did -r extban-creator 20
  did -b extban-creator 22-23
  did -r extban-creator 23
  did -era extban-creator 19 nick!*@*.isp.btroadband.com
  extban.cobbletogether
}

on *:DIALOG:extban-creator:sclick:7: set %extban.listmode +b | extban.cobbletogether
on *:DIALOG:extban-creator:sclick:8: set %extban.listmode +e | extban.cobbletogether
on *:DIALOG:extban-creator:sclick:9: set %extban.listmode +I | extban.cobbletogether

on *:DIALOG:extban-creator:edit:4: {
  if ($did(extban-creator, 4).text !isnum 1-999) {
    set %extban.timer 28
  }

  else { set %extban.timer $did(extban-creator, 4).text }
  extban.cobbletogether
}

on *:DIALOG:extban-creator:sclick:18: {
  ;; Enable Extended Selectors
  did -e extban-creator 20,30
  did -ra extban-creator 19 extended_criteria*

  ;; Load the list
  did -a extban-creator 20 Account Name
  did -a extban-creator 20 Channel [Optional Prefix]
  did -a extban-creator 20 Country Code
  did -a extban-creator 20 Security Group
  did -a extban-creator 20 Real Name [Use _ for Spaces]
  did -a extban-creator 20 Certificate Fingerprint [No Wildcards]
  did -a extban-creator 20 Provider ID [AS Number]
  if ($did(extbaN-creator,12).state == 0) { did -a extban-creator 20 Inherited From Channel }
  did -c extban-creator 20 1
  extban.cobbletogether
}

on *:DIALOG:extban-creator:sclick:23: {
  extban.cobbletogether
}

on *:DIALOG:extban-creator:sclick:20: {
  ; Reset Country Code Selection
  did -b extban-creator 22-23
  did -r extban-creator 23

  ; Set Selector Variable
  set %extban.selector $did(extban-creator,20).sel

  if (%extban.selector == 2) { did -ra extban-creator 19 #channel_name }
  if (%extban.selector == 5) { did -ra extban-creator 19 $read($mircdirincorrigo-syx\docu-mental\radio-rental.tr) $+ _ $+ $read($mircdirincorrigo-syx\docu-mental\chicken-oriental.tr) }
  if (%extban.selector == 7) { did -ra extban-creator 19 $r(0,9) $+ $r(0,9) $+ $r(0,9) $+ $r(0,9) $+ $r(0,9) }
  if (%extban.selector == 8) {
    did -bu extban-creator 8
    did -u extban-creator 9
    did -h extban-creator 14
    did -c extban-creator 7
    %extban.listmode = +b
  did -ra extban-creator 19 #bans_or_invites }


  if (%extban.selector != 8) {
    did -e extban-creator 8
    did -v extban-creator 14
  }

  ; Load Selector
  extban.loadselect
  extban.cobbletogether
}

alias -l extban.loadselect {
  if (%extban.selector == 3) {
    did -e extban-creator 22-23
    did -mra extban-creator 19 extended_criteria

    ;; populate country codelist
    extban.countrycodelist

  }
  else { did -n extban-creator 19 }
}

alias -l extban.countrycodelist {
  set %extb.counter 1
  set %extb.file incorrigo-syx\geo-ip\full-geoip.syx
  set %extb.lines $lines(%extb.file)
  while (%extb.counter <= %extb.lines) {
    set %extb.thisline $read(%extb.file, n, %extb.counter)
    did -a extban-creator 23 $gettok(%extb.thisline,2,44) $gettok(%extb.thisline,1,44)
    inc %extb.counter
  }
  did -c extban-creator 23 1
}

on *:DIALOG:extban-creator:sclick:12: {
  if ($did(extban-creator,12).state == 1) {
    ;; bring combo box into being
    did -e extban-creator 11
    did -c extban-creator 11 1

    ; the first item in the list is only [+b]
    ; so do this every time manually
    did -h extban-creator 14-15
    did -v extban-creator 13
    did -bu extban-creator 8-9
    did -ec extban-creator 7
    set %extban.listmode +b
  }

  else {
    ;; put list away
    did -ub extban-creator 11
    did -ubr extban-creator 26

    ;; put forward channel thing away
    did -b extban-creator 28
    did -m extban-creator 29

    ;; remove any restriction
    did -e extban-creator 7-9
    ;;did -c extban-creator 7
    did -v extban-creator 13-15
  }
  extban.cobbletogether
}

on *:DIALOG:extban-creator:sclick:11: {
  set %extban.action $did(extban-creator,11).sel

  if (%extban.action == 4 || %extban.action == 5) {
    ; these are +e only / disable +I and +b
    did -bu extban-creator 7,9
    %extban.listmode = +e
    did -h extban-creator 13,15
    did -ec extban-creator 8
    did -v extban-creator 14
  }

  else {
    ; anything else you can click on is only +b
    %extban.listmode = +b
    did -h extban-creator 14-15
    did -v extban-creator 13
    did -bu extban-creator 8-9
    did -ec extban-creator 7
  }
  extban.loadaction %extban.action
  extban.cobbletogether
}

on *:DIALOG:extban-creator:sclick:26 {
  extban.cobbletogether
}

alias -l extban.loadaction {
  ;; set the next list up based on which action selected
  ;; start by re-disablement / clearing it completely
  did -br extban-creator 26
  did -b extban-creator 28
  did -m extban-creator 29

  if (%extban.action == 3) {
    set %extban.listmode +b
    did -e extban-creator 28-29
    did -nra extban-creator 29 #channelname
  }

  if (%extban.action == 4) {
    ; bypass message restricting modes
    set %extban.listmode +e
    did -e extban-creator 26
    did -a extban-creator 26 External Messages to Channel [+n]
    did -a extban-creator 26 Speak While Moderated [+m / +M]
    did -a extban-creator 26 Bypass Server Word Censoring [+G]
    did -a extban-creator 26 Use Prohibited Text Formatting [+S / +c]
    did -a extban-creator 26 Allow Notices to Channel [+T]
    did -c extban-creator 26 1
  }

  if (%extban.action == 5) {
    ;immune to channel +f or +F flood defences
    set %extban.listmode +e
    did -e extban-creator 26
    did -a extban-creator 26 Channel CTCPs [c]
    did -a extban-creator 26 Mass Channel Joining [j]
    did -a extban-creator 26 Loads of Nickname Changes [n]
    did -a extban-creator 26 [Overall] Channel Notice / Message Flood [m]
    did -a extban-creator 26 Channel Operator Messaging /KNOCK Flood [k]
    did -a extban-creator 26 [Individual] Channel Text Flood[t]
    did -a extban-creator 26 [Individual] Repetitious Messages [r]
    did -a extban-creator 26 Bypass All Channel Flood Defences [*]
    did -c extban-creator 26 1
  }
}

alias extban.cobbletogether {
  set %extban.concat $null

  ;;set ban timer
  if (%extban.timed) { %extban.concat = %extban.concat $+ ~t: $+ %extban.timer $+ : }

  ;; ban / except only modifier
  if ($did(extban-creator, 12).state == 1) {
    if (%extban.action == 1) { %extban.concat = %extban.concat $+ ~q: }
    if (%extban.action == 2) { %extban.concat = %extban.concat $+ ~n: }
    if (%extban.action == 3) { %extban.concat = %extban.concat $+ ~f: $+ $did(extban-creator, 29).text $+ : }
    if (%extban.action == 4) {
      %extban.concat = %extban.concat $+ ~m:
      if ($did(extban-creator, 26).sel == 1) { %extban.concat = %extban.concat $+ external: }
      if ($did(extban-creator, 26).sel == 2) { %extban.concat = %extban.concat $+ moderated: }
      if ($did(extban-creator, 26).sel == 3) { %extban.concat = %extban.concat $+ censor: }
      if ($did(extban-creator, 26).sel == 4) { %extban.concat = %extban.concat $+ color: }
      if ($did(extban-creator, 26).sel == 5) { %extban.concat = %extban.concat $+ notice: }
    }
    if (%extban.action == 5) {
      %extban.concat = %extban.concat $+ ~F:
      if ($did(extban-creator, 26).sel == 1) { %extban.concat = %extban.concat $+ c: }
      if ($did(extban-creator, 26).sel == 2) { %extban.concat = %extban.concat $+ j: }
      if ($did(extban-creator, 26).sel == 3) { %extban.concat = %extban.concat $+ n: }
      if ($did(extban-creator, 26).sel == 4) { %extban.concat = %extban.concat $+ m: }
      if ($did(extban-creator, 26).sel == 5) { %extban.concat = %extban.concat $+ k: }
      if ($did(extban-creator, 26).sel == 6) { %extban.concat = %extban.concat $+ t: }
      if ($did(extban-creator, 26).sel == 7) { %extban.concat = %extban.concat $+ r: }
      if ($did(extban-creator, 26).sel == 8) { %extban.concat = %extban.concat $+ *: }
    }
    if (%extban.action == 6) {
      %extban.concat = %extban.concat $+ ~j:
    }
  }
  if ($did(extban-creator, 17).state == 1) {
    ;; this is a classic user mask entry
    %extban.concat = %extban.concat $+ $did(extban-creator, 19).text
  }
  if ($did(extban-creator, 18).state == 1) {
    ;; this is an extended ban alternative / geo-ip criteria selection
    if (%extban.selector == 1) { %extban.concat = %extban.concat $+ ~a: }
    if (%extban.selector == 2) { %extban.concat = %extban.concat $+ ~c: }
    if (%extban.selector == 3) {
      %extban.concat = %extban.concat $+ ~C:
      ;; lazily make "$1" country (or should i say ... cuntry ... hahaha)
      justeasier $did(extban-creator, 23).text
      goto countryfinishesit
    }
    ;; otherwise it must surely be one of these
    if (%extban.selector == 4) { %extban.concat = %extban.concat $+ ~G: }
    if (%extban.selector == 5) {
      %extban.concat = %extban.concat $+ ~r:
    }
    if (%extban.selector == 6) {
      %extban.concat = %extban.concat $+ ~S:
    }
    if (%extban.selector == 7) {
      %extban.concat = %extban.concat $+ ~A:
    }

    if (%extban.selector == 8) {
      %extban.concat = %extban.concat $+ ~i:
    }
    %extban.concat = %extban.concat $+ $did(extban-creator, 19).text
  }
  :countryfinishesit
  %extban.setmode = %extban.listmode %extban.concat
  did -ra extban-creator 25 %extban.setmode

  did -v extban-creator 34-36
}

alias -l justeasier {
  %extban.concat = %extban.concat $+ $1
}

on *:DIALOG:extban-creator:sclick:31: {
  text-reader incorrigo-syx\docu-mental\how-banlist-targeting-works.tr Understanding "Extended Ban" Targeting / List Functionality
}

;; Explanation of list modes for the uninitiated
on *:DIALOG:extban-creator:sclick:13: {
  .timer -m 1 150 noop $decision(ogu¿incorrigo syx $pipe about ban list¿An entry is put on the channel ban list using mode +b $+ $crlf $crlf $+ If you match an entry on the ban list $+ $c you will not be allowed to join or do certain things on the channel $+ $crlf $crlf $+ Some extended bans allow you to be on the channel $+ $c and affect you in other ways)
}

on *:DIALOG:extban-creator:sclick:14: {
  .timer -m 1 150 noop $decision(ogu¿incorrigo syx $pipe exception list¿An entry is put on the exception list using mode +e $+ $crlf $crlf $+ When someone matches an entry on the exception list $+ $c they will not be affected by any entry that's on the ban list $+ $crlf $crlf $+ Being on the exception list doesn't allow you to join the channel if it is invite only)
}

on *:DIALOG:extban-creator:sclick:15: {
  .timer -m 1 150 noop $decision(ogu¿incorrigo syx $pipe invite list¿An entry is put on the invite list using mode +I $+ $crlf $crlf $+ When someone matches an entry on the invite list $+ $c they will be able to freely join the channel when it is invite only [+i] $+ $crlf $crlf $+ Being on the invite list does not let you join the channel if you are currently banned)
}
