;;; INCORRIGO SYX DIGITAL COMMUNICATION SYSTEMS
;; AWAY / AUTO-AWAY MANAGER

dialog away-manager {
  title "incorrigo syx | away settings"
  size -1 -1 161 158
  option dbu
  box "Away Manager", 1, 4 4 152 82
  check "Auto-Away after", 2, 9 14 53 10
  edit "5", 3, 63 13 18 12, read limit 2
  link "+", 4, 84 10 7 8
  link "--", 5, 84 18 7 8
  text "minute[s]", 6, 94 15 30 8, center
  text "Auto Away Reason:", 7, 10 30 51 8
  edit "", 8, 64 28 75 12, autohs limit 359
  box "Set Away Now", 9, 4 42 152 44
  text "Message:", 10, 9 52 26 8
  edit "", 11, 34 50 115 12, autohs limit 359
  button "Set Aw&ay", 12, 33 68 51 12, flat
  button "Come Bac&k", 13, 88 68 51 12, flat
  button "E&xit", 14, 105 142 50 12, default flat ok cancel
  box "Away Reminder", 15, 5 88 151 49
  text "Auto-reply with away status on:", 16, 9 99 85 8
  check "Private Conversation", 17, 8 110 66 10
  check "Whisper (Notice)", 18, 8 121 66 10
  check "Mention in Channel", 19, 78 109 60 10
  check "CTCP Request", 20, 78 120 66 10
}

alias awaymgr {
  noop $dialog(away-manager, away-manager, u)
}

on *:CONNECT: {
  ;; timer will be deactivated when connection closed
  .timer $+ $server $+ . $+ $me 0 6 checkidle
  ronnik isaway 0
}

alias away {
  ;; connection-specific function added to the away command
  if ($1 == $null) {
    ;; no message = no longer away
    away
    ronnik isaway 0
  }

  else {
    ;; won't interfere with auto away
    away $1-
    ronnik isaway 1
  }
}

alias -l checkidle {
  if (%away.auto) {
    if ($away == $false) {
      if ($idle > $calc(%away.idletime * 60)) {
        ;; set away if idle time exceeds X * 60
        away auto away: %away.areason
        ronnik isaway 0
        if ($dialog(away-manager)) {
          did -b away-manager 12
          did -e away-manager 13
        }
      }
    }

    if ($away == $true && $ironnik(isaway) == 0) {
      if ($idle < $calc(%away.idletime * 60)) {
        away

        if ($dialog(away-manager)) {
          did -b away-manager 12
          did -e away-manager 13
        }
      }
    }
  }

  if (!%away.auto) {
    if ($away == $true && !$ironnik(isaway)) {
      away
    }
  }
  :barrymore
}

on *:DIALOG:away-manager:edit:8: {
  if ($len($did(away-manager, 8).text) >= 2) {
    %away.areason = $did(away-manager, 8).text
  }
}

on *:DIALOG:away-manager:edit:11: {
  if ($len($did(away-manager, 11).text) >= 2) {
    %away.reason = $did(away-manager, 11).text
  }
}

on *:DIALOG:away-manager:sclick:12: {
  away %away.reason
  ronnik isaway 1
  did -b away-manager 12
  did -e away-manager 13
}

on *:DIALOG:away-manager:sclick:13: {
  if ($away == $true) { away }
  ronnik isaway 0
  did -b away-manager 13
  did -e away-manager 12
}

on *:DIALOG:away-manager:sclick:17: {
  %away.on-msg = $did(away-manager, 17).state
}

on *:DIALOG:away-manager:sclick:18: {
  %away.on-notice = $did(away-manager, 18).state
}

on *:DIALOG:away-manager:sclick:19: {
  %away.on-mention = $did(away-manager, 19).state
}

on *:DIALOG:away-manager:sclick:20: {
  %away.on-ctcp = $did(away-manager, 20).state
}

on *:DISCONNECT: { ronnik isaway 0 }

on *:DIALOG:away-manager:sclick:2: {
  set %away.auto $did(away-manager, 2).state
}

on *:DIALOG:away-manager:init:0: {
  if (%away.auto == $null) {
    set %away.auto 1
  }

  if (!%away.areason || %away.areason == $false) {
    ;; separate reason for auto-away
    %away.areason = auto away: saession is idle.
  }

  if (%away.idletime == $null || %away.idletime == $false) {
    ;; idle duration [minutes]
    %away.idletime = 5
  }

  if (!%away.reason || %away.reason == $false) {
    ;; general away reason vary between session
    %away.reason = $awaymsg
  }

  if (%away.on-notice == $null || %away.on-notice == $false) {
    %away.on-notice = 1
  }

  if (%away.on-msg == $null || %away.on-msg == $false) {
    %away.on-msg = 1
  }

  if (%away.on-mention == $null || %away.on-mention == $false) {
    %away.on-mention = 1
  }

  if (%away.on-ctcp == $null || %away.on-ctcp == $false) {
    %away.on-ctcp = 1
  }

  if (%away.on-msg) { did -c away-manager 17 }
  if (%away.on-notice) { did -c away-manager 18 }
  if (%away.on-mention) { did -c away-manager 19 }
  if (%away.on-ctcp) { did -c away-manager 20 }

  if (%away.auto == 1) { did -c away-manager 2 }
  did -ra away-manager 3 %away.idletime
  did -ra away-manager 8 %away.areason
  did -ra away-manager 11 %away.reason

  if ($away == $true) { did -b away-manager 12 }
  if ($away == $false) { did -b away-manager 13 }
}


on *:DIALOG:away-manager:sclick:4: {
  if (%away.idletime >= 15) {
    halt
  }

  else {
    inc %away.idletime
    did -ra away-manager 3 %away.idletime
  }
}

on *:DIALOG:away-manager:dclick:4: {
  if (%away.idletime >= 15) {
    halt
  }

  else {
    inc %away.idletime
    did -ra away-manager 3 %away.idletime
  }
}

on *:DIALOG:away-manager:sclick:5: {
  if (%away.idletime <= 2) {
    halt
  }

  else {
    dec %away.idletime
    did -ra away-manager 3 %away.idletime
  }
}

on *:DIALOG:away-manager:dclick:5: {
  if (%away.idletime <= 2) {
    halt
  }

  else {
    dec %away.idletime
    did -ra away-manager 3 %away.idletime
  }
}

on *:START: {
  if (!%away.auto || %away.auto == $false) {
    ;; idle away [or auto-away] setting
    %away.auto = 1
  }

  if (!%away.areason || %away.areason == $false) {
    ;; separate reason for auto-away
    %away.areason = catching files.
  }

  if (!%away.idletime || %away.idletime == $false) {
    ;; idle duration [minutes]
    %away.idletime = 5
  }

  if (%away.reason == $null || %away.reason == $false) {
    ;; general away reason vary between session
    %away.reason = not here right now. please try again later.
  }

  if (!%away.on-notice || %away.on-notice == $False) {
    %away.on-notice = 1
  }

  if (!%away.on-msg || %away.on-msg == $False) {
    %away.on-msg = 1
  }

  if (!%away.on-mention || %away.on-mention == $False) {
    %away.on-mention = 1
  }

  if (!%away.on-ctcp || %away.on-ctcp == $False) {
    %away.on-ctcp = 1
  }
}

on 1:NOTICE:*:?: {
  ;; private notices only
  if ($away == $true && %away.on-notice && $timer($server $+ . $+ $me $+ .wap) == $null) {
    if (reply: !isin $1-) {
      .notice $nick auto reply: away from keyboard  $+ $chr(91) $+  $+ $awaymsg  $+ $chr(93) $+   $+ $chr(91) $+ timer: $duration($awaytime) $+  $+ $chr(93) $+ 
      .timer $+ $server $+ . $+ $me $+ .wap 1 8 noop
    }
  }
}


on 1:TEXT:*:?: {
  ;; respond to open query
  if ($away == $true && %away.on-msg && $timer($server $+ . $+ $me $+ .wap) == $null) {
    if (reply: !isin $1-) {
      msg $nick auto reply: i'm currently away from keyboard  $+ $chr(91) $+  $+ $awaymsg $+  $+ $chr(93) $+   $+ $chr(91) $+ timer: $duration($awaytime) $+  $+ $chr(93) $+ 
      .timer $+ $server $+ . $+ $me $+ .wap 1 8 noop
    }
  }
}



on 1:TEXT:*:#: {
  ;; nick mentioned in channel
  if ($msgtags(batch).key == $null) {
    if ($me isin $strip($1-) && $away == $true && $timer($server $+ . $+ $me $+ .wap) == $null) {
      .notice $nick auto reply: i'm away from keyboard  $+ $chr(91) $+  $+ $awaymsg $+  $+ $chr(93) $+   $+ $chr(91) $+ timer: $duration($awaytime) $+  $+ $chr(93) $+ 
      .timer $+ $server $+ . $+ $me $+ .wap 1 8 noop    
    }
  }
}

ctcp 1:*:?: {
  if ($away == $true && %away.on-ctcp && $timer($server $+ . $+ $me $+ .wap) == $null) {
    .ctcpreply $nick AWAY away from keyboard  $+ $chr(91) $+  $+ $awaymsg $+  $+ $chr(93) $+   $+ $chr(91) $+ timer: $duration($awaytime) $+  $+ $chr(93) $+  
    .timer $+ $server $+ . $+ $me $+ .wap 1 8 noop
  }
}
